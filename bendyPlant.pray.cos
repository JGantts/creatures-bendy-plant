**CAOS2PRAY
*# Pray-File "bendyPlant.agents"
*# DS-Name "Bendy Plant"
*# Agent Animation File = "bendyPlant.c16"
*# Agent Sprite First Image = 3
*# desc = "From the mysterious world of Lihgarn. The seasons there are extreme and the plant has had to adapt.\n\nManually injected seeds are inedible and invisible to creatures.\n\nImages by Linda.\nCode by Zzzzoot."

*# attach bendyPlant.c16

new: simp 2 3 24401 "bendyPlant" 1 0 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 15
accg 6
fric 95
setv va00 game "CreatorY"
subv va00 hght
mvto game "CreatorX" va00
tick 20

scrp 2 3 24401 9
	doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
		tick 20
		stop
	endi
	doif rtyp room ownr >= 5 and rtyp room ownr <= 7
* room type is soil
		inst
* calculate location of new plant agent
		setv va00 posl
		subv va00 20
		setv va01 post
		subv va01 108
		new: simp 2 4 24401 "bendyPlant" 26 1 292
		mira rand 0 1
		setv ov00 rand 0 1
		tick 20
		mvto va00 va01
		kill ownr
		slow
	else
* room type is not soil
	endi
endm

scrp 2 4 24401 9
	doif ov00 = 0
		doif pose = 0
			pose 1
		elif pose = 1
			pose 2
		elif pose = 2
			pose 3
			tick 100
		elif pose = 3
			pose 9
		elif pose = 9
			pose 12
		elif pose = 12
			pose 15
			tick 20
		elif pose = 15
			pose 17
		elif pose = 17
			pose 19
			tick 100
		elif pose = 19
			pose 22
			tick 20
		elif pose = 22
			pose 24
		elif pose = 24
			pose 3
			tick 100
		else
			dbg: outv pose
		endi
	else
		doif pose = 0
			pose 1
		elif pose = 1
			pose 2
		elif pose = 2
			pose 4
			tick 100
		elif pose = 4
			pose 10
		elif pose = 10
			pose 13
		elif pose = 13
			pose 16
			tick 20
		elif pose = 16
			pose 18
		elif pose = 18
			pose 20
			tick 100
		elif pose = 20
			pose 23
			tick 20
		elif pose = 23
			pose 25
		elif pose = 25
			pose 4
			tick 100
		else
			dbg: outv pose
		endi
	endi
endm

rscr
enum 2 3 24401
	kill targ
next
enum 2 4 24401
	kill targ
next

endm