**CAOS2PRAY
*# Pray-File "bendyPlant.agents"
*# DS-Name "Bendy Plant"
*# Agent Animation File = "bendyPlant.c16"
*# Agent Sprite First Image = 3
*# desc = "From the mysterious world of Leyhorn. The seasons there are extreme and these plants have had to adapt.\n\nManually injected seeds are inedible and invisible to creatures.\n\nImages by Linda.\nCode by Zzzzoot."

*# attach bendyPlant.c16 bendyPlant.catalogue

new: simp 2 3 24401 "bendyPlant" 1 0 447
*First seed is inedible and invisible to creatures. Otherwise it'd be hard to start a plant.
setv va00 195
addv va00 16
attr va00
*bhvr 48
perm 64
elas 15
accg 6
fric 95
setv va00 game "CreatorY"
subv va00 hght
mvto game "CreatorX" va00
tick 20

scrp 2 3 24401 9
	doif carr <> null or fall <> 0
* we're being carried or we're falling
*	probably not a good time to sprout.
*	but we are in a rush so hurry it up
		tick 20
		stop
	endi
	doif rtyp room ownr >= 5 and rtyp room ownr <= 7
* room type is soil
		inst
* calculate location of new plant agent
		setv va00 posl
		subv va00 20
		setv va01 post
		subv va01 108
		new: comp 2 4 24401 "bendyPlant" 26 1 292
*	random image (flower type)
		setv ov00 rand 0 1

*		Spring color
		setv va10 rean "bendyPlantColor Spring"
		subv va10 1
		sets va11 read "bendyPlantColor Spring" rand 0 va10
		sets va12 subs va11 1 3
		sets va13 subs va11 5 3
		sets va14 subs va11 9 3
		setv ov10 stoi va12
		setv ov11 stoi va13
		setv ov12 stoi va14

*		Summer color
		setv va10 rean "bendyPlantColor Summer"
		subv va10 1
		sets va11 read "bendyPlantColor Summer" rand 0 va10
		sets va12 subs va11 1 3
		sets va13 subs va11 5 3
		sets va14 subs va11 9 3
		setv ov13 stoi va12
		setv ov14 stoi va13
		setv ov15 stoi va14

*		Autunm color
		setv va10 rean "bendyPlantColor Autumn"
		subv va10 1
		sets va11 read "bendyPlantColor Autumn" rand 0 va10
		sets va12 subs va11 1 3
		sets va13 subs va11 5 3
		sets va14 subs va11 9 3
		setv ov16 stoi va12
		setv ov17 stoi va13
		setv ov18 stoi va14

*		Winter color
		setv va10 rean "bendyPlantColor Winter"
		subv va10 1
		sets va11 read "bendyPlantColor Winter" rand 0 va10
		sets va12 subs va11 1 3
		sets va13 subs va11 5 3
		sets va14 subs va11 9 3
		setv ov19 stoi va12
		setv ov20 stoi va13
		setv ov21 stoi va14

		tick 20
		mvto va00 va01
		kill ownr
		slow
	else
* room type is not soil
	endi
endm

scrp 2 4 24401 9
	doif pose = 0
* stem grow spring
		pose 1
		wait 20
		pose 2
*set image offset for using the alternative mature stalk
		base ov00
		wait 20
		pose 3
*		else
*			should already be spring stem.
*			should also be only ~1 tick after became spring stem
	endi
	wait 50
*set image offset for using the alternative mature stalk
	base ov00
* flower grow spring-fall
	pat: dull 1 "bendyPlant" 27 0 0 1
	part 1
	tint mv10 mv11 mv12 128 128
	frat 4
	wait 20
	pose 1
	wait 20
	pose 2
	wait 40
* flower close
	pose 3
	wait 20
	pose 4
	wait 40
* flower shoot
	anim [5 5 6 4]
	over
	wait 40
* flower close
	pose 3
	wait 20
	pose 2
	wait 50
* stem summer
	part 0
	pose 9
	part 1
	tint mv13 mv14 mv15 128 128
	wait 100
* stem autumn
	part 0
	pose 12
	part 1
	tint mv16 mv17 mv18 128 128
	wait 100
* flower wilt
	part 1
	pose 7
	wait 20
* flower fade out
	setv va00 0
	loop
		alph va00 1
		addv va00 8
		wait 1
	untl va00 >= 256
	pat: kill 1
* stem freeze
	part 0
	pose 15
	wait 20
	pose 17
	wait 20
* stem winter
	pose 19
	wait 50
* flower grow winter
	pat: dull 1 "bendyPlant" 27 0 0 1
	part 1
	tint mv19 mv20 mv21 128 128
	frat 4
	wait 20
	pose 1
	wait 20
	pose 2
	wait 50
* flower wilt
	part 1
	pose 7
	wait 20
* flower fade out
	setv va00 0
	loop
		alph va00 1
		addv va00 8
		wait 1
	untl va00 >= 256
	pat: kill 1
* stem melt
	part 0
	pose 22
	wait 20
	pose 24
	wait 20
* stem spring
	pose 3
	wait 100
	base 0
	tick 1
endm

rscr
enum 2 3 24401
	kill targ
next
enum 2 4 24401
	kill targ
next

endm